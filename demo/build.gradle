apply plugin: 'com.android.application'
apply from: '../build-config/gradle-quality.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.evernote.android.job.demo"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles 'proguard.cfg'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
            signingConfig signingConfigs.debug
        }

        gcm.initWith(buildTypes.debug)
        gcm {
            applicationIdSuffix ".gcm"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError true

        htmlOutput file("$project.buildDir/reports/lint/lint.html")
        xmlOutput file("$project.buildDir/reports/lint/lint.xml")
    }
}

dependencies {
    compile project(':library')
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibVersion"

    compile "com.facebook.stetho:stetho:$rootProject.ext.stethoVersion"

    gcmCompile "com.google.android.gms:play-services-gcm:$rootProject.ext.playServicesVersion"
}
